plugins {
    id 'dev.architectury.loom' version '1.1-SNAPSHOT'
    id 'net.darkhax.curseforgegradle' version '1.+'
    id "com.modrinth.minotaur" version "2.+"
    id 'maven-publish'
}

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = "${project.mod_version}-mc${project.minecraft_version}"
group = project.maven_group

loom {
    forge {
        mixinConfigs = [
                "reeses_sodium_options.mixins.json"
        ]
    }
}

repositories {
    repositories {
        maven {
            name = "Modrinth"
            url = "https://api.modrinth.com/maven"
        }
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    forge "net.minecraftforge:forge:${project.forge_version}"

    modImplementation "maven.modrinth:rubidium:${project.rubidium_version}"
}

processResources {
    inputs.property "version", project.version

    filesMatching("META-INF/mods.toml") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 17
}

java {
    withSourcesJar()
}

jar {
    from "LICENSE"
    manifest.attributes([
            "Implementation-Title"  : project.archivesBaseName,
            "Implementation-Version": project.version
    ])
}

// configure the maven publication
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    // select the repositories you want to publish to
    repositories {
        // uncomment to publish to the local maven
    }
}

modrinth {
    token = System.getenv("MODRINTH_TOKEN")

    projectId = "textrues-rubidium-options"
    versionNumber = "${project.version}"
    versionType = "release"
    uploadFile = remapJar
    additionalFiles = [sourcesJar]
    gameVersions = ["1.19.4"]
    loaders = ["forge"]
    changelog = "changelog.md"
    dependencies {
        required.project "rubidium"
        optional.project "rubidium-extra"
        optional.project "oculus"
    }
}

task publishCurseForge(type: net.darkhax.curseforgegradle.TaskPublishCurseForge) {
    apiToken = System.getenv("CURSEFORGE_TOKEN")
    disableVersionDetection()

    def mainFile = upload(663125, remapJar)
    mainFile.displayName = "${project.version}"
    mainFile.releaseType = "release"
    mainFile.changelog = file("changelog.md")
    mainFile.changelogType = "markdown"
    mainFile.addModLoader("Forge")
    mainFile.addJavaVersion("Java 17", "Java 18")
    mainFile.addGameVersion("1.19.4")
    mainFile.addRequirement("rubidium")
    mainFile.addOptional("rubidium-extra")
    mainFile.addOptional("oculus")

    def sourcesFile = mainFile.withAdditionalFile(sourcesJar)
    sourcesFile.changelog = file("changelog.md")
}
